OBJECT Table 4 Currency
{
  OBJECT-PROPERTIES
  {
    Date=25/10/16;
    Time=12:00:00;
    Version List=NAVW110.00,NAVES10.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               CustLedgEntry@1000 : Record 21;
               VendLedgEntry@1001 : Record 25;
             BEGIN
               CustLedgEntry.SETRANGE(Open,TRUE);
               CustLedgEntry.SETRANGE("Currency Code",Code);
               IF NOT CustLedgEntry.ISEMPTY THEN
                 ERROR(Text002,CustLedgEntry.TABLECAPTION,TABLECAPTION,Code);

               VendLedgEntry.SETRANGE(Open,TRUE);
               VendLedgEntry.SETRANGE("Currency Code",Code);
               IF NOT VendLedgEntry.ISEMPTY THEN
                 ERROR(Text002,VendLedgEntry.TABLECAPTION,TABLECAPTION,Code);

               CurrExchRate.SETRANGE("Currency Code",Code);
               CurrExchRate.DELETEALL;
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;
             END;

    CaptionML=[ENU=Currency;
               ESP=Divisa];
    LookupPageID=Page5;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;OnValidate=BEGIN
                                                                IF Symbol = '' THEN
                                                                  Symbol := ResolveCurrencySymbol(Code);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ESP=C¢digo];
                                                   NotBlank=Yes }
    { 2   ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESP=Fecha £lt. modificaci¢n];
                                                   Editable=No }
    { 3   ;   ;Last Date Adjusted  ;Date          ;CaptionML=[ENU=Last Date Adjusted;
                                                              ESP=Fecha £lt. ajuste divisas];
                                                   Editable=No }
    { 6   ;   ;Unrealized Gains Acc.;Code20       ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Unrealized Gains Acc.");
                                                              END;

                                                   CaptionML=[ENU=Unrealized Gains Acc.;
                                                              ESP=Cta. dif. pos. no realizadas] }
    { 7   ;   ;Realized Gains Acc. ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Realized Gains Acc.");
                                                              END;

                                                   CaptionML=[ENU=Realized Gains Acc.;
                                                              ESP=Cta. dif. pos. realizadas] }
    { 8   ;   ;Unrealized Losses Acc.;Code20      ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Unrealized Losses Acc.");
                                                              END;

                                                   CaptionML=[ENU=Unrealized Losses Acc.;
                                                              ESP=Cta. dif. neg. no realizadas] }
    { 9   ;   ;Realized Losses Acc.;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Realized Losses Acc.");
                                                              END;

                                                   CaptionML=[ENU=Realized Losses Acc.;
                                                              ESP=Cta. dif. neg. realizadas] }
    { 10  ;   ;Invoice Rounding Precision;Decimal ;InitValue=0,01;
                                                   OnValidate=BEGIN
                                                                IF "Amount Rounding Precision" <> 0 THEN
                                                                  IF "Invoice Rounding Precision" <> ROUND("Invoice Rounding Precision","Amount Rounding Precision") THEN
                                                                    FIELDERROR(
                                                                      "Invoice Rounding Precision",
                                                                      STRSUBSTNO(Text000,"Amount Rounding Precision"));
                                                              END;

                                                   CaptionML=[ENU=Invoice Rounding Precision;
                                                              ESP=Precisi¢n redondeo factura];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 12  ;   ;Invoice Rounding Type;Option       ;CaptionML=[ENU=Invoice Rounding Type;
                                                              ESP=Tipo redondeo factura];
                                                   OptionCaptionML=[ENU=Nearest,Up,Down;
                                                                    ESP=M s cercano,Superior,Inferior];
                                                   OptionString=Nearest,Up,Down }
    { 13  ;   ;Amount Rounding Precision;Decimal  ;InitValue=0,01;
                                                   OnValidate=BEGIN
                                                                IF "Amount Rounding Precision" <> 0 THEN BEGIN
                                                                  "Invoice Rounding Precision" := ROUND("Invoice Rounding Precision","Amount Rounding Precision");
                                                                  IF "Amount Rounding Precision" > "Invoice Rounding Precision" THEN
                                                                    "Invoice Rounding Precision" := "Amount Rounding Precision";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Amount Rounding Precision;
                                                              ESP=Prec. redondeo importe];
                                                   DecimalPlaces=2:5;
                                                   MinValue=0 }
    { 14  ;   ;Unit-Amount Rounding Precision;Decimal;
                                                   InitValue=0,00001;
                                                   CaptionML=[ENU=Unit-Amount Rounding Precision;
                                                              ESP=Prec. redondeo precio-prod.];
                                                   DecimalPlaces=0:9;
                                                   MinValue=0 }
    { 15  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ESP=Descripci¢n] }
    { 17  ;   ;Amount Decimal Places;Text5        ;InitValue=2:2;
                                                   OnValidate=BEGIN
                                                                GLSetup.CheckDecimalPlacesFormat("Amount Decimal Places");
                                                              END;

                                                   CaptionML=[ENU=Amount Decimal Places;
                                                              ESP=N§ decimales para importes];
                                                   NotBlank=Yes }
    { 18  ;   ;Unit-Amount Decimal Places;Text5   ;InitValue=2:5;
                                                   OnValidate=BEGIN
                                                                GLSetup.CheckDecimalPlacesFormat("Unit-Amount Decimal Places");
                                                              END;

                                                   CaptionML=[ENU=Unit-Amount Decimal Places;
                                                              ESP=N§ decimales para precio-prod.];
                                                   NotBlank=Yes }
    { 19  ;   ;Customer Filter     ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Customer;
                                                   CaptionML=[ENU=Customer Filter;
                                                              ESP=Filtro cliente] }
    { 20  ;   ;Vendor Filter       ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Vendor;
                                                   CaptionML=[ENU=Vendor Filter;
                                                              ESP=Filtro proveedor] }
    { 21  ;   ;Global Dimension 1 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Global Dimension 1 Filter;
                                                              ESP=Filtro dimensi¢n global 1];
                                                   CaptionClass='1,3,1' }
    { 22  ;   ;Global Dimension 2 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Global Dimension 2 Filter;
                                                              ESP=Filtro dimensi¢n global  2];
                                                   CaptionClass='1,3,2' }
    { 23  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              ESP=Filtro fecha] }
    { 24  ;   ;Cust. Ledg. Entries in Filter;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Cust. Ledger Entry" WHERE (Customer No.=FIELD(Customer Filter),
                                                                                                 Currency Code=FIELD(Code)));
                                                   CaptionML=[ENU=Cust. Ledg. Entries in Filter;
                                                              ESP=Movs. cliente en filtro];
                                                   Editable=No }
    { 25  ;   ;Customer Balance    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Detailed Cust. Ledg. Entry".Amount WHERE (Customer No.=FIELD(Customer Filter),
                                                                                                              Initial Entry Global Dim. 1=FIELD(Global Dimension 1 Filter),
                                                                                                              Initial Entry Global Dim. 2=FIELD(Global Dimension 2 Filter),
                                                                                                              Posting Date=FIELD(Date Filter),
                                                                                                              Currency Code=FIELD(Code)));
                                                   CaptionML=[ENU=Customer Balance;
                                                              ESP=Saldo cliente];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 26  ;   ;Customer Outstanding Orders;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Sales Line"."Outstanding Amount" WHERE (Document Type=CONST(Order),
                                                                                                            Bill-to Customer No.=FIELD(Customer Filter),
                                                                                                            Currency Code=FIELD(Code),
                                                                                                            Shortcut Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                            Shortcut Dimension 2 Code=FIELD(Global Dimension 2 Filter)));
                                                   AccessByPermission=TableData 110=R;
                                                   CaptionML=[ENU=Customer Outstanding Orders;
                                                              ESP=Pedidos pendientes cliente];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 27  ;   ;Customer Shipped Not Invoiced;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Sales Line"."Shipped Not Invoiced" WHERE (Document Type=CONST(Order),
                                                                                                              Bill-to Customer No.=FIELD(Customer Filter),
                                                                                                              Currency Code=FIELD(Code),
                                                                                                              Shortcut Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                              Shortcut Dimension 2 Code=FIELD(Global Dimension 2 Filter)));
                                                   AccessByPermission=TableData 110=R;
                                                   CaptionML=[ENU=Customer Shipped Not Invoiced;
                                                              ESP=Enviado a cliente no factur.];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 28  ;   ;Customer Balance Due;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Detailed Cust. Ledg. Entry".Amount WHERE (Customer No.=FIELD(Customer Filter),
                                                                                                              Initial Entry Global Dim. 1=FIELD(Global Dimension 1 Filter),
                                                                                                              Initial Entry Global Dim. 2=FIELD(Global Dimension 2 Filter),
                                                                                                              Initial Entry Due Date=FIELD(Date Filter),
                                                                                                              Posting Date=FIELD(UPPERLIMIT(Date Filter)),
                                                                                                              Currency Code=FIELD(Code),
                                                                                                              Excluded from calculation=CONST(No)));
                                                   CaptionML=[ENU=Customer Balance Due;
                                                              ESP=Saldo vencido cliente];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 29  ;   ;Vendor Ledg. Entries in Filter;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Vendor Ledger Entry" WHERE (Vendor No.=FIELD(Vendor Filter),
                                                                                                  Currency Code=FIELD(Code)));
                                                   CaptionML=[ENU=Vendor Ledg. Entries in Filter;
                                                              ESP=Movs. proveedor en filtro];
                                                   Editable=No }
    { 30  ;   ;Vendor Balance      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Detailed Vendor Ledg. Entry".Amount WHERE (Vendor No.=FIELD(Vendor Filter),
                                                                                                                Initial Entry Global Dim. 1=FIELD(Global Dimension 1 Filter),
                                                                                                                Initial Entry Global Dim. 2=FIELD(Global Dimension 2 Filter),
                                                                                                                Posting Date=FIELD(Date Filter),
                                                                                                                Currency Code=FIELD(Code)));
                                                   CaptionML=[ENU=Vendor Balance;
                                                              ESP=Saldo proveedor];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 31  ;   ;Vendor Outstanding Orders;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Purchase Line"."Outstanding Amount" WHERE (Document Type=CONST(Order),
                                                                                                               Pay-to Vendor No.=FIELD(Vendor Filter),
                                                                                                               Currency Code=FIELD(Code),
                                                                                                               Shortcut Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                               Shortcut Dimension 2 Code=FIELD(Global Dimension 2 Filter)));
                                                   AccessByPermission=TableData 120=R;
                                                   CaptionML=[ENU=Vendor Outstanding Orders;
                                                              ESP=Pedidos pendientes proveedor];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 32  ;   ;Vendor Amt. Rcd. Not Invoiced;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Purchase Line"."Amt. Rcd. Not Invoiced" WHERE (Document Type=CONST(Order),
                                                                                                                   Pay-to Vendor No.=FIELD(Vendor Filter),
                                                                                                                   Currency Code=FIELD(Code),
                                                                                                                   Shortcut Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                                                                                                                   Shortcut Dimension 2 Code=FIELD(Global Dimension 2 Filter)));
                                                   AccessByPermission=TableData 120=R;
                                                   CaptionML=[ENU=Vendor Amt. Rcd. Not Invoiced;
                                                              ESP=Recibido de prov. no facturado];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 33  ;   ;Vendor Balance Due  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Detailed Vendor Ledg. Entry".Amount WHERE (Vendor No.=FIELD(Vendor Filter),
                                                                                                                Initial Entry Global Dim. 1=FIELD(Global Dimension 1 Filter),
                                                                                                                Initial Entry Global Dim. 2=FIELD(Global Dimension 2 Filter),
                                                                                                                Initial Entry Due Date=FIELD(Date Filter),
                                                                                                                Posting Date=FIELD(UPPERLIMIT(Date Filter)),
                                                                                                                Currency Code=FIELD(Code),
                                                                                                                Excluded from calculation=CONST(No)));
                                                   CaptionML=[ENU=Vendor Balance Due;
                                                              ESP=Saldo vencido proveedor];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 34  ;   ;Customer Balance (LCY);Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Detailed Cust. Ledg. Entry"."Amount (LCY)" WHERE (Customer No.=FIELD(Customer Filter),
                                                                                                                      Initial Entry Global Dim. 1=FIELD(Global Dimension 1 Filter),
                                                                                                                      Initial Entry Global Dim. 2=FIELD(Global Dimension 2 Filter),
                                                                                                                      Posting Date=FIELD(Date Filter),
                                                                                                                      Currency Code=FIELD(Code)));
                                                   CaptionML=[ENU=Customer Balance (LCY);
                                                              ESP=Saldo cliente (DL)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 35  ;   ;Vendor Balance (LCY);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Detailed Vendor Ledg. Entry"."Amount (LCY)" WHERE (Vendor No.=FIELD(Vendor Filter),
                                                                                                                        Initial Entry Global Dim. 1=FIELD(Global Dimension 1 Filter),
                                                                                                                        Initial Entry Global Dim. 2=FIELD(Global Dimension 2 Filter),
                                                                                                                        Posting Date=FIELD(Date Filter),
                                                                                                                        Currency Code=FIELD(Code)));
                                                   CaptionML=[ENU=Vendor Balance (LCY);
                                                              ESP=Saldo proveedor (DL)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 40  ;   ;Realized G/L Gains Account;Code20  ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Realized G/L Gains Account");
                                                              END;

                                                   CaptionML=[ENU=Realized G/L Gains Account;
                                                              ESP=Cta. aj. pos. C/G realizados] }
    { 41  ;   ;Realized G/L Losses Account;Code20 ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                CheckGLAcc("Realized G/L Losses Account");
                                                              END;

                                                   CaptionML=[ENU=Realized G/L Losses Account;
                                                              ESP=Cta. aj. neg. C/G realizados] }
    { 44  ;   ;Appln. Rounding Precision;Decimal  ;CaptionML=[ENU=Appln. Rounding Precision;
                                                              ESP=Precisi¢n redondeo liquidaci¢n];
                                                   MinValue=0;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 45  ;   ;EMU Currency        ;Boolean       ;CaptionML=[ENU=EMU Currency;
                                                              ESP=Divisa UME] }
    { 46  ;   ;Currency Factor     ;Decimal       ;CaptionML=[ENU=Currency Factor;
                                                              ESP=Factor divisa];
                                                   DecimalPlaces=0:6;
                                                   Editable=No }
    { 47  ;   ;Residual Gains Account;Code20      ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Residual Gains Account;
                                                              ESP=Cta. aj. residual pos.] }
    { 48  ;   ;Residual Losses Account;Code20     ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Residual Losses Account;
                                                              ESP=Cta. aj. residual neg.] }
    { 50  ;   ;Conv. LCY Rndg. Debit Acc.;Code20  ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Conv. LCY Rndg. Debit Acc.;
                                                              ESP=Cta. red. neg. conv. en DL] }
    { 51  ;   ;Conv. LCY Rndg. Credit Acc.;Code20 ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Conv. LCY Rndg. Credit Acc.;
                                                              ESP=Cta. red. pos. conv. en DL] }
    { 52  ;   ;Max. VAT Difference Allowed;Decimal;OnValidate=BEGIN
                                                                IF "Max. VAT Difference Allowed" <> ROUND("Max. VAT Difference Allowed","Amount Rounding Precision") THEN
                                                                  ERROR(
                                                                    Text001,
                                                                    FIELDCAPTION("Max. VAT Difference Allowed"),"Amount Rounding Precision");

                                                                "Max. VAT Difference Allowed" := ABS("Max. VAT Difference Allowed");
                                                              END;

                                                   CaptionML=[ENU=Max. VAT Difference Allowed;
                                                              ESP=M x. diferencia IVA permitida];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 53  ;   ;VAT Rounding Type   ;Option        ;CaptionML=[ENU=VAT Rounding Type;
                                                              ESP=Tipo redondeo IVA];
                                                   OptionCaptionML=[ENU=Nearest,Up,Down;
                                                                    ESP=M s cercano,Superior,Inferior];
                                                   OptionString=Nearest,Up,Down }
    { 54  ;   ;Payment Tolerance % ;Decimal       ;CaptionML=[ENU=Payment Tolerance %;
                                                              ESP=% Tolerancia pago];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=No }
    { 55  ;   ;Max. Payment Tolerance Amount;Decimal;
                                                   CaptionML=[ENU=Max. Payment Tolerance Amount;
                                                              ESP=M x. importe tolerancia pago];
                                                   MinValue=0;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=Code }
    { 56  ;   ;Symbol              ;Text10        ;CaptionML=[ENU=Symbol;
                                                              ESP=S¡mbolo] }
    { 7000000;;Bill Groups - Collection;Boolean   ;CaptionML=[ENU=Bill Groups - Collection;
                                                              ESP=Remesas al cobro] }
    { 7000001;;Bill Groups - Discount;Boolean     ;CaptionML=[ENU=Bill Groups - Discount;
                                                              ESP=Remesas al dto.] }
    { 7000002;;Payment Orders      ;Boolean       ;CaptionML=[ENU=Payment Orders;
                                                              ESP=àrdenes pago] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Code,Description                         }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=must be rounded to the nearest %1;ESP=se debe redondear al %1 m s cercano';
      Text001@1001 : TextConst 'ENU=%1 must be rounded to the nearest %2.;ESP=Se debe redondear %1 al m s cercano %2.';
      CurrExchRate@1002 : Record 330;
      GLSetup@1003 : Record 98;
      Text002@1004 : TextConst '@@@=1 either customer or vendor ledger entry table 2 name co currency table 3 currencency code;ENU=There is one or more opened entries in the %1 table using %2 %3.;ESP=Hay una o m s entradas abiertas en la tabla %1 que usan %2 %3.';
      IncorrectEntryTypeErr@1005 : TextConst 'ENU=Incorrect Entry Type %1.;ESP=Tipo de movimiento %1 incorrecto.';

    PROCEDURE InitRoundingPrecision@2();
    BEGIN
      GLSetup.GET;
      IF GLSetup."Amount Rounding Precision" <> 0 THEN
        "Amount Rounding Precision" := GLSetup."Amount Rounding Precision"
      ELSE
        "Amount Rounding Precision" := 0.01;
      IF GLSetup."Unit-Amount Rounding Precision" <> 0 THEN
        "Unit-Amount Rounding Precision" := GLSetup."Unit-Amount Rounding Precision"
      ELSE
        "Unit-Amount Rounding Precision" := 0.000001;
      "Max. VAT Difference Allowed" := GLSetup."Max. VAT Difference Allowed";
      "VAT Rounding Type" := GLSetup."VAT Rounding Type";
      "Invoice Rounding Precision" := GLSetup."Inv. Rounding Precision (LCY)";
      "Invoice Rounding Type" := GLSetup."Inv. Rounding Type (LCY)";
    END;

    LOCAL PROCEDURE CheckGLAcc@1(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
      END;
    END;

    PROCEDURE VATRoundingDirection@3() : Text[1];
    BEGIN
      CASE "VAT Rounding Type" OF
        "VAT Rounding Type"::Nearest:
          EXIT('=');
        "VAT Rounding Type"::Up:
          EXIT('>');
        "VAT Rounding Type"::Down:
          EXIT('<');
      END;
    END;

    PROCEDURE InvoiceRoundingDirection@4() : Text[1];
    BEGIN
      CASE "Invoice Rounding Type" OF
        "Invoice Rounding Type"::Nearest:
          EXIT('=');
        "Invoice Rounding Type"::Up:
          EXIT('>');
        "Invoice Rounding Type"::Down:
          EXIT('<');
      END;
    END;

    PROCEDURE CheckAmountRoundingPrecision@5();
    BEGIN
      TESTFIELD("Unit-Amount Rounding Precision");
      TESTFIELD("Amount Rounding Precision");
    END;

    PROCEDURE GetGainLossAccount@6(DtldCVLedgEntryBuf@1000 : Record 383) : Code[20];
    BEGIN
      CASE DtldCVLedgEntryBuf."Entry Type" OF
        DtldCVLedgEntryBuf."Entry Type"::"Unrealized Loss":
          BEGIN
            TESTFIELD("Unrealized Losses Acc.");
            EXIT("Unrealized Losses Acc.");
          END;
        DtldCVLedgEntryBuf."Entry Type"::"Unrealized Gain":
          BEGIN
            TESTFIELD("Unrealized Gains Acc.");
            EXIT("Unrealized Gains Acc.");
          END;
        DtldCVLedgEntryBuf."Entry Type"::"Realized Loss":
          BEGIN
            TESTFIELD("Realized Losses Acc.");
            EXIT("Realized Losses Acc.");
          END;
        DtldCVLedgEntryBuf."Entry Type"::"Realized Gain":
          BEGIN
            TESTFIELD("Realized Gains Acc.");
            EXIT("Realized Gains Acc.");
          END;
        ELSE
          ERROR(IncorrectEntryTypeErr,DtldCVLedgEntryBuf."Entry Type");
      END;
    END;

    PROCEDURE GetCurrencySymbol@8() : Text[10];
    BEGIN
      IF Symbol <> '' THEN
        EXIT(Symbol);

      EXIT(Code);
    END;

    PROCEDURE ResolveCurrencySymbol@9(CurrencyCode@1000 : Code[10]) : Text[10];
    VAR
      Currency@1001 : Record 4;
      PoundChar@1002 : Char;
      EuroChar@1003 : Char;
      YenChar@1004 : Char;
    BEGIN
      IF Currency.GET(CurrencyCode) THEN
        IF Currency.Symbol <> '' THEN
          EXIT(Currency.Symbol);

      PoundChar := 163;
      YenChar := 165;
      EuroChar := 8364;

      CASE CurrencyCode OF
        'AUD','BND','CAD','FJD','HKD','MXN','NZD','SBD','SGD','USD':
          EXIT('$');
        'GBP':
          EXIT(FORMAT(PoundChar));
        'DKK','ISK','NOK','SEK':
          EXIT('kr');
        'EUR':
          EXIT(FORMAT(EuroChar));
        'CNY','JPY':
          EXIT(FORMAT(YenChar));
      END;

      EXIT('');
    END;

    PROCEDURE Initialize@7(CurrencyCode@1000 : Code[10]);
    BEGIN
      IF CurrencyCode <> '' THEN
        GET(CurrencyCode)
      ELSE
        InitRoundingPrecision;
    END;

    BEGIN
    END.
  }
}

